#!/usr/bin/env zsh
getPipe() {
  local dir
  local servername

  if [[ -n "$TMUX" && -n "$TMUX_PANE" ]]; then
    servername="t$(tmux list-panes -t "$TMUX_PANE" -F '#S' | head -n1)"
  else
    dir="$(git rev-parse --show-toplevel 2>/dev/null || pwd)"
    servername="s${dir//\//__}"
  fi

  echo "/tmp/nvim-${servername}.pipe"
}

getTTy() {
  local pid="${1}"

  if [[ "$pid" == "1" ]]; then
    return 1
  fi

  local tty
  tty=$(ps -p "$pid" -o "tty" | tail -1 | sed 's@ *$@@')

  if [[ "$tty" == "??" || "$tty" == "?" ]]; then
    local ppid
    ppid=$(ps -p "$pid" -o "ppid" | tail -1 | sed 's@ *$@@')
    getTTy "$ppid"
    return
  fi

  echo "$tty"
}

goToInstance() {
  if [[ -n "$TMUX" && -n "$TMUX_PANE" ]]; then
    local socket="${1}"
    local pid
    pid="$(lsof -t "$socket" 2>/dev/null | head -1)"
    if [[ -z "$pid" ]]; then
      return 1
    fi

    local tty
    tty=$(getTTy "$pid")

    if [[ -z "$tty" ]]; then
      return 1
    fi

    local tmux_path
    tmux_path=$(tmux list-panes -s -F'#I.#P,#{pane_tty}' | grep "${tty}\$" | cut -d ',' -f1)

    if [[ -z "$tmux_path" ]]; then
      return 1
    fi

    tmux select-pane -t "$tmux_path" && tmux select-window -t "${tmux_path%%.*}"
  fi
}


socket="$(getPipe)"

if [[ -e "$socket" ]]; then
  if [[ $# -gt 0 ]]; then
    # Convert relative paths to absolute paths
    local -a abs_paths
    for arg in "$@"; do
      if [[ -e "$arg" ]]; then
        # If file/directory exists, get absolute path
        abs_paths+=("$(realpath "$arg")")
      else
        # If doesn't exist yet (new file), resolve relative to current dir
        if [[ "$arg" = /* ]]; then
          # Already absolute
          abs_paths+=("$arg")
        else
          # Make it absolute
          abs_paths+=("$PWD/$arg")
        fi
      fi
    done
    nvim --server "$socket" --remote-silent "${abs_paths[@]}"
  else
    echo "NVIM already running on $socket"
  fi

  goToInstance "$socket"
else
  nvim --listen "$socket" "$@"
fi
